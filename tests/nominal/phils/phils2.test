// This version of the faulty philosophers use only two bits to codify the state
// of a philosopher, this reduces the state space of the examples
// a enumeration type will be added in next versions of the tool
// !s0!s1 : thinking
// !s0 s1: hungry
// s0!s1: eating

Enum STATE = {thinking,hungry,eating};

Global fork0 : BOOL;
Global fork1 : BOOL;



Process OddPhil(forkL: BOOL, forkR:BOOL){
   
   s : STATE;
   hasL, hasR : BOOL;
   Initial: s == thinking && !hasL && !hasR && forkR && forkL;
   Normative: !(hasR && !hasL);

   [hungry] s == thinking -> s = hungry;
   [getLeft] s == hungry && forkL && !hasL && !hasR  -> forkL=false, hasL=true;
   [getRight] s == hungry && hasL && forkR && !hasR -> forkR = false, hasR=true;
   [eating] s == hungry && hasL && hasR -> s = eating; 
   [thinking] s == eating -> s = thinking, forkL=true, forkR=true, hasR=false, hasL=false;
}

Process EvenPhil(forkL: BOOL, forkR:BOOL){

   s : STATE;
   hasL, hasR : BOOL;
   Initial: s == thinking && !hasL && !hasR && forkR && forkL;
   Normative: !(hasL && !hasR); 

   [hungry] s == thinking -> s = hungry;
   [getRight] s == hungry && forkR && !hasL && !hasR  -> forkR=false, hasR=true; 
   [getLeft] s == hungry && hasR && forkL && !hasL -> forkL = false, hasL=true;
   [eating] s == hungry && hasL && hasR -> s = eating; 
   [thinking] s == eating -> s = thinking, forkL=true, forkR=true, hasR=false, hasL=false;
   
}


Main(){
 phil1:OddPhil;
 phil2:EvenPhil;


 run phil1(fork1, fork0);
 run phil2(fork0, fork1);
}
