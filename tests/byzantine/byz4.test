Global g1g2A,g1g3A,g1g4A: BOOL; //Commander(g1) attack messages
Global g2g3A,g2g4A: BOOL; //Lieutenant1(g2) attack messages
Global g3g2A,g3g4A: BOOL; //Lieutenant2(g3) attack messages
Global g4g2A,g4g3A: BOOL; //Lieutenant3(g4) attack messages

Global g1g2R,g1g3R,g1g4R: BOOL; //Commander(g1) retreat messages
Global g2g3R,g2g4R: BOOL; //Lieutenant1(g2) retreat messages
Global g3g2R,g3g4R: BOOL; //Lieutenant2(g3) retreat messages
Global g4g2R,g4g3R: BOOL; //Lieutenant3(g4) retreat messages

Global A2,A3,A4: BOOL; //The Attack decision of each lieutenant
Global R2,R3,R4: BOOL; //The Retreat decision of each lieutenant

Process Commander{
	s0,s1: BOOL;
	Initial: s0 && !s1;
	Normative: true;
		[sA] s0 -> g1g2A = true, g1g3A = true, g1g4A = true, s0= false, s1=true;
		[sR] s0 -> g1g2R = true, g1g3R = true, g1g4R = true, s0= false, s1=true;
}


Process Lieutenant(attack: BOOL, retreat: BOOL, fw1A: BOOL, fw2A:BOOL, fw1R:BOOL, fw2R:BOOL, a1:BOOL, a2:BOOL, r1:BOOL, r2:BOOL, dA:BOOL, dR:BOOL){
	//PARAMS: attack: attack order from commander, fw1A and fw2A: messages sent to other lieutenants, a1 and a2: messages received from other lieutenants, dA: decide to attack, the rest of params are similar but with retreat 
	s0,s1,s2, isBetrayer: BOOL;
	Initial: s0 && !s1 && !s2 && !isBetrayer;
	Normative: true;
		[fA] s0 && attack && !isBetrayer -> fw1A = true, fw2A= true, s0 = false, s1 = true;
		[fR] s0 && retreat && !isBetrayer -> fw1R = true, fw2R = true, s0 = false, s1 = true;
		[fA] s0 && attack && isBetrayer -> fw1R = true, fw2R= true, s0 = false, s1 = true;
		[fR] s0 && retreat && isBetrayer -> fw1A = true, fw2A= true, s0 = false, s1 = true;
		[Betray] faulty s0 && !isBetrayer -> isBetrayer = true; 
		[Attack] s1 && !isBetrayer && ((attack && a1)||(attack && a2)||(a1 && a2)) -> s1 = false, s2 = true, dA = true;
		[Retreat] s1 && !isBetrayer && ((retreat && r1)||(retreat && r2)||(r1 && r2)) -> s1 = false, s2 = true, dR = true;

}



Main(){
    g1:Commander;
    g2:Lieutenant;
    g3:Lieutenant;
    g4:Lieutenant;
    run g1();
    run g2(g1g2A,g1g2R,g2g3A,g2g4A,g2g3R,g2g4R,g3g2A,g4g2A,g3g2R,g4g2R,A2,R2);
    run g3(g1g3A,g1g3R,g3g2A,g3g4A,g3g2R,g3g4R,g2g3A,g4g3A,g2g3R,g4g3R,A3,R3);
    run g4(g1g4A,g1g4R,g4g3A,g4g2A,g4g3R,g4g2R,g2g4A,g3g4A,g2g4R,g3g4R,A4,R4);
}